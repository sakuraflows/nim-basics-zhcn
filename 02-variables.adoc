= 命名值(Naming values)



在编程中，为程序中的值赋予名称通常有助于我们追踪和管理数据。
例如，当向用户询问其姓名时，我们希望存储该姓名以便后续使用，避免每次需要进行相关计算时反复询问。

在示例 `pi = 3.14` 中，名称 `pi` 与值 `3.14` 相关联。
根据经验可知，变量 `pi` 的类型为（十进制）数字。

Another example would be `firstName = Alice`, where `firstName` is the name of a variable with the value `Alice`.
We would say that the type of this variable is a word.

编程语言中的机制与此类似。
这些名称分配包含它们的名称(_name_)、值(_value_)和类型(_type_)三个要素。




== 变量声明(Variable declaration)


Nim 是一种静态类型( https://en.wikipedia.org/wiki/Type_system#STATIC[statically typed] )编程语言，这意味着在使用值之前需要声明其赋值类型。（译者注：其实nim也可以自动推理，有的情况下无需声明。）

在Nim中，我们还会区分可以改变或可变的值与不可变的值(mutate)，但这一点稍后再详述。

我们可以使用 `var` 关键字声明一个变量（即可变赋值），只需通过以下语法指定其名称和类型（值可以稍后添加）：
[source]
----
var <name>: <type>
----

如果我们已经知道它的值，可以立即声明一个变量并为其赋值：
[source]
----
var <name>: <type> = <value>
----
NOTE: 尖括号（ `<>` ）用于表示可以更改的内容. +
所以 `<name>` 并非字面意义上的尖括号内的单词 `name` ，而是指代任意名称。


Nim 还具备类型推断( https://en.wikipedia.org/wiki/Type_inference[type inference] )能力：编译器能根据赋值自动推断变量类型，无需显式声明。
我们将在下一章深入探讨各种类型。

因此我们可以像这样不指定显式类型来声明变量:
[source]
----
var <name> = <value>
----


在 Nim 中，一个示例如下:
[source]
----
var a: int  # <1>
var b = 7   # <2>
----
<1> 变量 `a` 的类型为 `int` （整数），未显式设置值。(with no value explicitly set.)
<2> 变量 `b` 的值为 `7` ，其类型被自动推断为整数。


在命名时，选择对程序有意义的名称非常重要。
简单地命名为 `a` 、 `b` 、 `c` 等很快就会变得混乱。
名称中不能使用空格，因为这会将其分割成两部分。
因此，如果你选择的名称由多个单词组成，通常的做法是使用 `camelCase` 风格书写（注意名称的首字母应为小写）。

但请注意，Nim对大小写和下划线不敏感，这意味着 `helloWorld` 和 `hello_world` 会被视为相同的名称。
唯一的例外是首字符，它对大小写敏感。
名称中还可以包含数字和其他 UTF-8 字符，甚至可以使用表情符号（如果你愿意的话），但请记住，你和其他人可能需要输入这些字符。

{nbsp}

无需为每个变量单独输入 `var`，可以在同一个 `var` 块中声明多个变量（类型不必相同）。
在 Nim 中，块是指具有相同缩进（首字符前空格数相同）的代码部分，默认缩进级别为两个空格。
在 Nim 程序中，此类块随处可见，不仅用于命名赋值。

[source]
----
var
  c = -11
  d = "Hello"
  e = '!'
----

NOTE: 在 Nim 中不允许使用制表符进行缩进. +
你可以设置代码编辑器将按下 Tab 转换为任意数量的空格. +
在 VS Code 中，默认设置是将 `Tab` 转换为四个空格。
这可以通过在设置中（ `Ctrl+,` ）配置 `"editor.tabSize": 2` 轻松覆盖。


如前所述，变量是可变的，即:它们的值可以（多次）改变，但变量的类型必须与声明时保持一致。

[source]
----
var f = 7           <1>

f = -3              <2>
f = 19
f = "Hello" # error <3><4>
----
<1> 变量 `f` 的初始值为 `7` ，其类型被推断为 `int` 。
<2> 首先将 `f` 的值更改为 `-3` ，然后再改为 `19` 。这两个值都是整数，与原值类型相同。
<3> 尝试将 `f` 的值更改为 "Hello" 时会产生错误，因为 `Hello` 不是数字，这将导致 `f` 的类型从整数变为字符串。
<4> `# error` 是注释。Nim代码中的注释写在 `#` 字符之后。同一行中该字符后的所有内容都会被忽略。




== 不可变赋值(Immutable assignment)

与使用 var 关键字声明的变量不同，Nim中还存在另外两种赋值类型，其值不可更改，一种是用 `const` 关键字声明的，另一种则是用 `let` 关键字声明的。


=== 常量(Const)

使用 `const` 关键字声明的不可变赋值，其值必须在编译时（程序运行前）已知。

例如，我们可以将重力加速度声明为 `const g = 9.81` 或将圆周率声明为 `const pi = 3.14` ，因为我们预先知道它们的值，并且这些值在程序执行过程中不会改变。

[source]
----
const g = 35
g = -27         # error <1>

var h = -5
const i = h + 7 # error <2>
----
<1> 常量的值不可更改。
<2> 变量 `h` 在编译时不会被求值（它是一个变量，其值在程序执行过程中可能改变），因此常量 `i` 的值在编译时无法确定，这将引发错误。

在某些编程语言中，常量的名称通常以 `ALL_CAPS` 书写。
而在Nim中，常量的书写方式与其他变量无异。



=== Let

使用 `let` 声明的不可变赋值不需要在编译时已知，它们的值可以在程序执行的任何时间设置，但一旦设置，它们的值就不能更改。

[source]
----
let j = 35
j = -27 # error <1>

var k = -5
let l = k + 7   <2>
----
<1> 不可变对象的值不能被改变。
<2> 与上面的 `const` 示例不同，这个可以工作。

在实际应用中，你会更频繁地见到或使用 `let` 而非 `const` 。

虽然你可以用 `var` 处理所有情况，但默认应选择 `let` 。
仅对那些需要被修改的变量使用 `var` 。
